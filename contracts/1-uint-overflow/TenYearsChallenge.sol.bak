pragma solidity ^0.7.0;

contract TenYearsChallenge {
    mapping(address => bool) public isOperator;

    address public owner;
    Contribution[] queue;
    uint256 head;

    struct Contribution {
        uint256 amount;
        uint256 unlockTimestamp;
    }

    modifier onlyOperator() {
        require(isOperator[msg.sender], "only operator");
        _;
    }

    constructor(address[] memory _operators) public payable {
        owner = msg.sender;
        for (uint256 i = 0; i < _operators.length; i++) {
            require(_operators[i] != address(0), "zero _operator");
            isOperator[_operators[i]] = true;
        }

        // start challenge
        queue.push(Contribution(msg.value, block.timestamp + 10 * 365 days));
    }


    function isComplete() public view returns (bool) {
        return address(this).balance == 0;
    }

    function upsert(uint256 index, uint256 timestamp) public payable onlyOperator {
        require(msg.sender == owner);

        if (index >= head && index < queue.length) {
            // Update existing contribution amount without updating timestamp.
            Contribution storage contribution = queue[index];
            contribution.amount += msg.value;
        } else {
            // Append a new contribution. Require that each contribution unlock
            // at least 1 day after the previous one.
            require(timestamp >= queue[queue.length - 1].unlockTimestamp + 1 days);

            Contribution memory contribution;
            contribution.amount = msg.value;
            contribution.unlockTimestamp = timestamp;
            queue.push(contribution);
        }
    }

    function withdraw(uint256 index) public onlyOperator {
        require(msg.sender == owner);
        require(block.timestamp >= queue[index].unlockTimestamp);

        // Withdraw this and any earlier contributions.
        uint256 total = 0;
        for (uint256 i = head; i <= index; i++) {
            total += queue[i].amount;

            // Reclaim storage.
            delete queue[i];
        }

        // Move the head of the queue forward so we don't have to loop over
        // already-withdrawn contributions.
        head = index + 1;

        msg.sender.transfer(total);
    }
}
